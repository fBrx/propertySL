grammar com.github.fbrx.propertysl.PropertySL with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate propertySL "http://www.github.com/fbrx/propertysl/PropertySL"

Model:
	packages += Package*;

Package:
	(commentlines += COMMENT)*
	'package' name = FQN '{'
		supportedLocales = SupportedLocales?
		
		(packages += Package |
		properties += Property)*
	'}'
;

terminal COMMENT:
	'#' !('\n')*
;

SupportedLocales:
	'SUPPORTED_LOCALES' '=' '{'
		locales += DefaultableLocale
		(',' locales += DefaultableLocale)*
	'}'
;

FQN:
	ID ('.' ID)*
;

Property:
	(commentlines += COMMENT)*
	key = PropertyKey '=' value = AbstractPropertyValue
;

PropertyKey:
	ID
;

AbstractPropertyValue:
	SimplePropertyValue | ComplexPropertyValue
;

SimplePropertyValue:
	value = STRING
;

ComplexPropertyValue:
	'{'
		(items += ComplexPropertyValueItem)+
	'}'
;

ComplexPropertyValueItem:
	(lang = LOCALE ':' value = SimplePropertyValue)
;

DefaultableLocale:
	(isDefault ?= 'default')? lang=LOCALE
;

terminal LOCALE:
	('a'..'z')('a'..'z')('_'('A'..'Z')('A'..'Z'))?
;