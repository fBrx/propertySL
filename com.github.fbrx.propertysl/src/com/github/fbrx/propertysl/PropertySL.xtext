grammar com.github.fbrx.propertysl.PropertySL with org.eclipse.xtext.common.Terminals

generate propertySL "http://www.github.com/fbrx/propertysl/PropertySL"

Model:
	packages += Package*;

Package:
	'package' name = FQN '{'
		supportedLocales = SupportedLocales?
		defaultLocale = DefaultLocale?
		
		(packages += Package |
		properties += Property)*
	'}'
;

SupportedLocales:
	'SUPPORTED_LOCALES' '=' '{'
		locales += LOCALE
		(',' locales += LOCALE)*
	'}'
;

FQN:
	ID ('.' ID)*
;

Property:
	key = PropertyKey '=' value = AbstractPropertyValue
;

PropertyKey:
	ID
;

AbstractPropertyValue:
	SimplePropertyValue | ComplexPropertyValue
;

SimplePropertyValue:
	value = STRING
;

ComplexPropertyValue:
	'{'
		(items += ComplexPropertyValueItem)+
	'}'
;

ComplexPropertyValueItem:
	(lang = LOCALE ':' value = SimplePropertyValue)
;

DefaultLocale:
	'DEFAULT_LOCALE' '=' lang = LOCALE
;

terminal LOCALE:
	('a'..'z')('a'..'z')('_'('A'..'Z')('A'..'Z'))?
;